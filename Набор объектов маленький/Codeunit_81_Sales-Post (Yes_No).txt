OBJECT Codeunit 81 Sales-Post (Yes/No)
{
  OBJECT-PROPERTIES
  {
    Date=18.12.13;
    Time=12:00:00;
    Version List=NAVW17.10,NAVRU7.10;
  }
  PROPERTIES
  {
    TableNo=36;
    OnRun=BEGIN
            SalesHeader.COPY(Rec);
            Code;
            Rec := SalesHeader;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=&Ship,&Invoice,Ship &and Invoice;RUS=&??????,&????? ???,?????? &? ????? ???';
      Text001@1001 : TextConst 'ENU=Do you want to post the %1?;RUS=????? %1?';
      Text002@1002 : TextConst 'ENU=&Receive,&Invoice,Receive &and Invoice;RUS=&???????,&????? ???,??????? &? ????? ???';
      SalesHeader@1003 : Record 36;
      UserSetup@1210002 : Record 91;
      Selection@1005 : Integer;
      Text14928@1210001 : TextConst 'ENU=Previe&w,P&ost;RUS=?&??????? ?????,?&??';
      PreviewEnabled@1210003 : Boolean;
      PreviewSelection@1210004 : Integer;

    LOCAL PROCEDURE Code@1();
    VAR
      SalesSetup@1002 : Record 311;
      SalesPostViaJobQueue@1000 : Codeunit 88;
      SalesPost@1210000 : Codeunit 80;
    BEGIN
      PreviewEnabled := FALSE;
      IF UserSetup.GET(USERID) THEN
        CASE SalesHeader."Document Type" OF
          SalesHeader."Document Type"::Invoice,
          SalesHeader."Document Type"::Order:
            PreviewEnabled := UserSetup."Enable Sales Invoice Preview";
          SalesHeader."Document Type"::"Credit Memo",
          SalesHeader."Document Type"::"Return Order":
            PreviewEnabled := UserSetup."Enable Sales Cr. Memo Preview";
        END;

      IF PreviewEnabled THEN BEGIN
        Selection := STRMENU(Text14928,1);
        IF Selection = 0 THEN
          EXIT;
        PreviewSelection := Selection;
      END ELSE
        PreviewSelection := 2;

      WITH SalesHeader DO BEGIN
        IF PreviewSelection = 2 THEN BEGIN // Post
          CASE "Document Type" OF
            "Document Type"::Order:
              BEGIN
                Selection := STRMENU(Text000,3);
                IF Selection = 0 THEN
                  EXIT;
                Ship := Selection IN [1,3];
                Invoice := Selection IN [2,3];
              END;
            "Document Type"::"Return Order":
              BEGIN
                Selection := STRMENU(Text002,3);
                IF Selection = 0 THEN
                  EXIT;
                Receive := Selection IN [1,3];
                Invoice := Selection IN [2,3];
              END ELSE
                IF NOT
                   CONFIRM(
                     Text001,FALSE,
                     "Document Type")
                THEN
                  EXIT;
          END;
          "Print Posted Documents" := FALSE;

          SalesSetup.GET;
          IF SalesSetup."Post with Job Queue" THEN
            SalesPostViaJobQueue.EnqueueSalesDoc(SalesHeader)
          ELSE
            CODEUNIT.RUN(CODEUNIT::"Sales-Post",SalesHeader);
        END ELSE BEGIN // Preview
          CASE "Document Type" OF
            "Document Type"::Order:
               Ship := TRUE;
            "Document Type"::"Return Order":
               Receive := TRUE;
          END;
          Invoice := TRUE;
          SalesPost.SetPreviewMode(TRUE,FALSE);
          SalesPost.RUN(SalesHeader);
        END;
      END;
    END;

    BEGIN
    END.
  }
}


