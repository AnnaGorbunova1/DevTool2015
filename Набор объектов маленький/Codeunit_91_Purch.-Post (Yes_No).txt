OBJECT Codeunit 91 Purch.-Post (Yes/No)
{
  OBJECT-PROPERTIES
  {
    Date=18.12.13;
    Time=12:00:00;
    Version List=NAVW17.10,NAVRU7.10;
  }
  PROPERTIES
  {
    TableNo=38;
    OnRun=BEGIN
            PurchHeader.COPY(Rec);
            Code;
            Rec := PurchHeader;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=&Receive,&Invoice,Receive &and Invoice;RUS=&???????,&????? ???,??????? &? ????? ???';
      Text001@1001 : TextConst 'ENU=Do you want to post the %1?;RUS=????? %1?';
      Text002@1002 : TextConst 'ENU=&Ship,&Invoice,Ship &and Invoice;RUS=&??????,&????? ???,?????? &? ????? ???';
      PurchHeader@1003 : Record 38;
      UserSetup@1210001 : Record 91;
      Selection@1005 : Integer;
      PreviewEnabled@1210003 : Boolean;
      PreviewSelection@1210002 : Integer;
      Text14928@1210004 : TextConst 'ENU=Previe&w,P&ost;RUS=?&??????? ?????,?&??';

    LOCAL PROCEDURE Code@1();
    VAR
      PurchSetup@1000 : Record 312;
      PurchPostViaJobQueue@1001 : Codeunit 98;
      PurchPost@1210000 : Codeunit 90;
    BEGIN
      PreviewEnabled := FALSE;
      IF UserSetup.GET(USERID) THEN
        CASE PurchHeader."Document Type" OF
          PurchHeader."Document Type"::Invoice,
          PurchHeader."Document Type"::Order:
            PreviewEnabled := UserSetup."Enable Purch. Invoice Preview";
          PurchHeader."Document Type"::"Credit Memo",
          PurchHeader."Document Type"::"Return Order":
            PreviewEnabled := UserSetup."Enable Purch. Cr. Memo Preview";
        END;
      IF PreviewEnabled THEN BEGIN
        Selection := STRMENU(Text14928,1);
        IF Selection = 0 THEN
          EXIT;
        PreviewSelection := Selection;
      END ELSE
        PreviewSelection := 2;

      WITH PurchHeader DO BEGIN
        IF PreviewSelection = 2 THEN BEGIN // Post
          CASE "Document Type" OF
            "Document Type"::Order:
              BEGIN
                Selection := STRMENU(Text000,3);
                IF Selection = 0 THEN
                  EXIT;
                Receive := Selection IN [1,3];
                Invoice := Selection IN [2,3];
              END;
            "Document Type"::"Return Order":
              BEGIN
                Selection := STRMENU(Text002,3);
                IF Selection = 0 THEN
                  EXIT;
                Ship := Selection IN [1,3];
                Invoice := Selection IN [2,3];
              END ELSE
                IF NOT
                   CONFIRM(
                     Text001,FALSE,
                     "Document Type")
                THEN
                  EXIT;
          END;
          "Print Posted Documents" := FALSE;

          IF PurchSetup."Post with Job Queue" THEN
            PurchPostViaJobQueue.EnqueuePurchDoc(PurchHeader)
          ELSE
            CODEUNIT.RUN(CODEUNIT::"Purch.-Post",PurchHeader);
        END ELSE BEGIN
          CASE "Document Type" OF
            "Document Type"::"Return Order":
               Ship := TRUE;
            "Document Type"::Order:
               Receive := TRUE;
          END;
          Invoice := TRUE;
          PurchPost.SetPreviewMode(TRUE,FALSE);
          PurchPost.RUN(PurchHeader);
        END;
      END;
    END;

    BEGIN
    END.
  }
}


